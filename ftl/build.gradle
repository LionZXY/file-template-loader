buildscript {
  repositories {
    maven { url "https://dl.bintray.com/jetbrains/intellij-plugin-service" }
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm'
  id "org.jetbrains.intellij"
}

repositories {
  mavenCentral()
}

dependencies {
  implementation project(":ftl-runtime")
  implementation project(":ftl-kts-runner")

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

intellij {
  version '202.6397.94'
  pluginName 'FileTemplate Loader'
  plugins 'Groovy', 'properties', 'java', "org.jetbrains.kotlin:1.4.21-release-IJ2020.2-1"
}

patchPluginXml {
  sinceBuild '171.0'
  pluginDescription 'Improved template engine for IntelliJ IDEA to generate templates, their groups and modules.\n' +
    'Supports the creation of multiple files organized in subdirectories, and insert code into existing files.\n' +
    'Allows you to use the parameters you want to enter when you create a template.'
}

tasks.create("copyJarToResources", Copy) {
  from configurations.runtimeClasspath
    .filter { File file ->
      file.name.contains("kotlin-script-runtime") ||
        file.name.contains("ftl-runtime") ||
        (file.name.contains("kotlin-stdlib") && !file.name.contains("kotlin-stdlib-common"))
    }
  into "src/main/resources"
  rename { String fileName ->
    fileName.replace("-${kotlinVersion}", "")
  }
}
tasks.findByName("jar").dependsOn("copyJarToResources")
